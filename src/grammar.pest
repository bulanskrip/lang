whitespaces = _{ (" " | "\t") }
num     = @{ subtract? ~ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
int   = @{ subtract? ~ ASCII_DIGIT+ }

operator   =  { calc | ternaryIf | ternaryElse | orCond | andCond | isType}
calc = _{or|and|add | nullOr | subtract | multiply | divide | power | unsgShiftLeft |unsgShiftRight|shiftLeft |shiftRight}
add        = @{ "+" }
subtract   = @{ "-" }
multiply   = @{ "*" }
divide     = @{ "/" }
power      = @{ "^" }
isType = @{whitespaces+~"is"~whitespaces+}
or = @{"|"}
and = @{"&"}
orCond = @{ "||" }
andCond  = @{ "&&" }
nullOr = @{"??"}
unsgShiftLeft = @{"<<<"}
shiftLeft = @{"<<"}
unsgShiftRight = @{">>>"}
shiftRight = @{">>"}
ternaryIf = @{"?"}
ternaryElse = @{":"}
// hes alone..
nullable = @{"?"}

bool = { true | false }
true    = { "true" }
false   = { "false" }

reserved = { const | "if" | "case" | "default" | "switch" | await | void | bool | "import" | new |"type" |"class"|"mod"|pub|"typecomp"|"enum"|"is"}
await    = @{ "await" }
void     = @{ "void" }
const    = @{ "const" }
fn       = @{ "fn" }
async    = @{ "async" }
new = @{"new"}
pub = @{"pub"}
literal = {str | void | bool | int|num | array | evaluate | inlineFnBlock}
value = {(literal|((new ~whitespaces+)~ ident~("." ~ ident)*)|ident) ~ (("." ~ ident) | (whitespaces*)~caller | index)*}
valueContainer             =  _{value ~ whitespaces* ~(operator ~ whitespaces* ~ valueContainer ~ whitespaces*)* }
identBase         = _{ (ASCII_ALPHA | "$" | "_" |int)* }
ident             = @{ !reserved ~ (ASCII_ALPHA | "$" | "_") ~ identBase }
caller            =  { "(" ~ whitespaces* ~ (valueContainer ~whitespaces* ~ ("," ~ whitespaces* ~ valueContainer ~ whitespaces*)*)? ~ ")" }
typeCaller        =  { "<" ~ whitespaces*~(typeContainer ~ whitespaces* ~("," ~whitespaces*~ typeContainer~whitespaces*)*)? ~ ">" }
argument = {typeContainer~ whitespaces+ ~ ident}
varargs = {".."~nullable?~whitespaces*~argument}
argumentsBase = _{blankWhitespaces~(argument ~ whitespaces*~(("," ~whitespaces*~ argument~whitespaces*)* ~ ("," ~whitespaces*~varargs~whitespaces*)?)?|(whitespaces*~varargs~whitespaces*))}
arguments = {"(" ~argumentsBase~ ")" }
typeArguments = {"<"~argumentsBase~">"}
index = @{"["~valueContainer~"]"}
importModule      =  { "import" ~ whitespaces* ~ str ~ whitespaces* ~ "to"~ whitespaces+ ~ ident }
evaluate          =  { "(" ~ valueContainer ~ ")" }

array = {"[" ~ whitespaces* ~ (valueContainer ~whitespaces* ~ ("," ~ whitespaces* ~ valueContainer ~ whitespaces*)*)? ~ "]"}
str_contentQuote = {(!"\"" ~ ANY)*}
str_contentApos = {(!"'" ~ ANY)*}
str_contentBacktick = {(!"`" ~ ANY)*}
str       = { ("\"" ~ str_contentQuote ~ "\"") | ("'" ~ str_contentApos ~ "'") | ("`" ~ str_contentBacktick ~ "`") }
typeValue = _{str|type|int|bool}
typeContainer = {typeValue~(whitespaces*~("|"~blankWhitespaces~typeValue)+)?}
type         =  { (ident ~ ("."~ident)* ~ typeCaller? ~ nullable?) | void }
newVariable  =  { decorators?~(pub ~ whitespaces+)? ~ (const~whitespaces+ )? ~ typeContainer ~ whitespaces+ ~ ident }
declareQuick =  { ident ~whitespaces*~ ":=" ~whitespaces*~ valueContainer }
declare      =  { ((value ~ !caller)|newVariable) ~whitespaces*~ calc? ~"=" ~whitespaces*~ valueContainer}
nl           = _{ NEWLINE | ";" }
block        =  {blankWhitespaces ~"{" ~ codes ~ blankWhitespaces ~ "}" ~blankWhitespaces}
customFnBlock = { (async ~ whitespaces+)? ~ typeContainer ~ whitespaces+ ~ ident ~whitespaces* ~ arguments? ~ block }
localFnBlock = {decorators?~(pub ~ whitespaces+)? ~ "fn" ~ whitespaces+ ~customFnBlock}
inlineFnBlock      =  { (async~ whitespaces+)? ~fn ~whitespaces* ~ arguments? ~ block }
newBlock   =  { !async~ "new" ~ whitespaces* ~ arguments?~ block }
ifBlock      =  { "if"~ whitespaces+ ~ valueContainer ~ block }
caseBlock    =  { "case" ~ whitespaces+  ~ valueContainer ~ block }
enums = {blankWhitespaces~(ident ~ blankWhitespaces ~("," ~ blankWhitespaces~ ident~blankWhitespaces)*)?~blankWhitespaces}
enumBlock = {decorators?~(pub~whitespaces+)?~"enum"~whitespaces+~ident~whitespaces*~"{"~enums~"}"}
defaultBlock =  { "default" ~whitespaces*~ block }
moduleBlock = {decorators?~(pub ~ whitespaces+)?~"mod"~whitespaces+~ident~whitespaces*~block}
caseContainer = {whitespaces*  ~ valueContainer ~ whitespaces* ~"{"~ (blankWhitespaces ~ (caseBlock|defaultBlock))* ~ blankWhitespaces ~ "}"}
switchBlock  =  { "switch" ~ caseContainer }
matchBlock  =  { "match" ~ caseContainer }
classBlock  =  { decorators?~(pub~whitespaces+)?~"class" ~ whitespaces+  ~ ident ~ (whitespaces* ~ ((nullable ~ whitespaces* ~ typeArguments)|(typeArguments ~ whitespaces* ~ nullable)|typeArguments) ~ whitespaces*)? ~ whitespaces* ~"{" ~whitespaces*~ (nl*~whitespaces* ~ (newBlock|customFnBlock) ~ whitespaces*)* ~ nl* ~whitespaces* ~ "}" }
implBlock = {(whitespaces* ~ ((nullable ~ whitespaces* ~ typeArguments)|(typeArguments ~ whitespaces* ~ nullable)|typeArguments) ~ whitespaces*)? ~ whitespaces* ~"{" ~ blankWhitespaces ~ ((customFnBlock) ~ blankWhitespaces) ~ "}"}
inlineType = {decorators?~(pub ~ whitespaces+)?~"type"~whitespaces+~ident~whitespaces*~"="~whitespaces*~typeContainer}
typecompFnBlock = {decorators?~(pub~whitespaces+)?~"typecomp"~whitespaces+~ident~whitespaces*~(arguments|typeArguments)?~block}
decorator = {"@"~whitespaces*~ ident ~ (whitespaces+ ~ valueContainer|whitespaces*)?}
decorators = {blankWhitespaces ~ decorator ~ (nl+ ~ whitespaces* ~ decorator ~ whitespaces*)*}
return = {"return"~whitespaces+~valueContainer}
comment = { ("--[[" ~ (!"--]]" ~ ANY)* ~ "--]]")|("--" ~ (!NEWLINE ~ ANY)*)}
code         =  { enumBlock | comment | return | matchBlock | typecompFnBlock |inlineType | localFnBlock | moduleBlock | importModule | declareQuick | declare | ifBlock | classBlock | switchBlock  | value }
// i hope this error will be fixed asap. this occurs on 2.6 ðŸ¤Œ codes        = _{blankWhitespaces ~ code ~(blankWhitespaces ~ code?)+}
codes        = _{(whitespaces* ~ nl* ~ whitespaces* ~ code)+}
// workaround for 2.6 and onward. (RULE+)?
// then on the code you MUST BE USING *, no, you cant use that + thingy
blankWhitespaces = _{(whitespaces+)? ~ (nl+)? ~ (whitespaces+)?}
program      =  { SOI ~ codes ~ EOI }